# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/shell/public/cpp/service.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

static_library("test_support") {
  sources = [
    "test_catalog_store.cc",
    "test_catalog_store.h",
  ]

  deps = [
    "//base",
    "//services/catalog:lib",
    "//url",
  ]
}

source_set("unittests") {
  testonly = true
  sources = [
    "background_shell_unittest.cc",
  ]

  deps = [
    ":test_app_bindings",
    ":test_support",
    "//base",
    "//services/shell/background:lib",
    "//services/shell/background:main",
    "//services/shell/public/cpp:sources",
    "//testing/gtest",
    "//url",
  ]

  data_deps = [
    ":test_app",
  ]
}

mojom("test_app_bindings") {
  sources = [
    "test.mojom",
  ]

  use_new_wrapper_types = false
}

service("test_app") {
  output_name = "background_shell_test_app"

  sources = [
    "test_service.cc",
  ]

  deps = [
    ":test_app_bindings",
    "//base",
    "//services/shell/public/cpp:sources",
    "//services/shell/public/interfaces",
  ]
}
