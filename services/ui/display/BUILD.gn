# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")

source_set("display") {
  sources = [
    "screen_manager.cc",
    "screen_manager.h",
    "screen_manager_delegate.h",
    "viewport_metrics.cc",
    "viewport_metrics.h",
  ]

  deps = [
    "//base",
    "//services/service_manager/public/cpp",
    "//ui/display",
    "//ui/gfx",
  ]

  if (use_ozone && is_chromeos) {
    sources += [
      "screen_manager_ozone.cc",
      "screen_manager_ozone.h",
    ]

    deps += [
      "//chromeos",
      "//services/ui/public/interfaces/display",
      "//skia",
      "//ui/display/manager",
      "//ui/ozone",
    ]
  } else {
    sources += [
      "screen_manager_stub.cc",
      "screen_manager_stub.h",
    ]
  }
}

if (use_ozone && is_chromeos) {
  test("display_service_unittests") {
    sources = [
      "run_all_unittests.cc",
      "screen_manager_ozone_unittests.cc",
    ]

    deps = [
      ":display",
      "//base",
      "//base/test:test_support",
      "//services/ui/common:task_runner_test_base",
      "//testing/gmock",
      "//testing/gtest",
      "//ui/base",
      "//ui/display",
      "//ui/events/devices",
      "//ui/gfx",
      "//ui/gfx:test_support",
      "//ui/ozone",
    ]

    data_deps = [
      "//ui/resources:ui_test_pak_data",
    ]
  }
}
