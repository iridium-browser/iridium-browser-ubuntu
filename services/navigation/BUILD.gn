# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//services/service_manager/public/tools/test/service_test.gni")
import("//testing/test.gni")
import("//tools/grit/repack.gni")

group("all") {
  testonly = true
  data_deps = [
    ":navigation",
    ":navigation_unittests",
  ]
}

source_set("navigation") {
  sources = [
    "navigation.cc",
    "navigation.h",
    "view_impl.cc",
    "view_impl.h",
  ]

  public_deps = [
    "//base",
    "//content/public/common",
    "//mojo/public/cpp/bindings",
    "//services/navigation/public/interfaces",
    "//services/service_manager/public/cpp",
    "//services/ui/public/interfaces",
    "//ui/aura",
  ]

  deps = [
    "//content/public/browser",
    "//skia",
    "//ui/gfx/geometry/mojo",
    "//ui/views",
    "//ui/views/controls/webview",
    "//ui/views/mus",
  ]
}

service_test("navigation_unittests") {
  sources = [
    "navigation_unittest.cc",
  ]

  catalog = ":navigation_unittests_catalog"

  deps = [
    "//base",
    "//mojo/public/cpp/bindings",
    "//services/navigation/public/interfaces",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp:service_test_support",
    "//services/service_manager/public/interfaces",
    "//testing/gtest",
  ]

  data_deps = [
    "//content/shell:content_shell",
    "//services/ui/test_wm",
  ]
}

service_manifest("manifest") {
  name = "navigation"
  source = "manifest.json"
}

service_manifest("unittest_manifest") {
  name = "navigation_unittests"
  source = "unittest_manifest.json"
}

catalog("navigation_unittests_catalog") {
  embedded_services = [ ":unittest_manifest" ]
  standalone_services = [
    ":manifest",
    "//services/ui:manifest",
    "//services/ui/test_wm:manifest",
  ]
}
