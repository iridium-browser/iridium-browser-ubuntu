# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "barcode_detection_impl.h",
    "face_detection_provider_impl.h",
    "shape_detection_service.cc",
    "shape_detection_service.h",
  ]

  if (is_mac) {
    sources += [
      "barcode_detection_impl_mac.h",
      "barcode_detection_impl_mac.mm",
      "detection_utils_mac.h",
      "detection_utils_mac.mm",
      "face_detection_impl_mac.h",
      "face_detection_impl_mac.mm",
    ]
    libs = [ "QuartzCore.framework" ]
  } else {
    sources += [
      "barcode_detection_impl.cc",
      "face_detection_provider_impl.cc",
    ]
  }

  deps = [
    "//mojo/public/cpp/bindings",
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]

  public_deps = [
    "//base",
    "//media/capture",
    "//services/service_manager/public/cpp",
    "//services/shape_detection/public/interfaces",
  ]
}

service_manifest("manifest") {
  name = "shape_detection"
  source = "manifest.json"
}

source_set("tests") {
  testonly = true
  sources = []
  if (is_mac) {
    sources += [
      "barcode_detection_impl_mac_unittest.mm",
      "face_detection_impl_mac_unittest.mm",
    ]

    libs = [
      "CoreFoundation.framework",
      "CoreGraphics.framework",
      "QuartzCore.framework",
    ]

    deps = [
      ":lib",
      "//base",
      "//skia",
      "//testing/gmock",
      "//testing/gtest",
      "//ui/gfx",
      "//ui/gl",
    ]
  }
}
