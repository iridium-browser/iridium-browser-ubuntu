# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/remoting_srcs.gni")

source_set("plugin") {
  sources =
      rebase_path(remoting_srcs_gypi_values.remoting_client_plugin_sources,
                  ".",
                  "//remoting")

  if (!is_chromeos) {
    sources -= [
      "normalizing_input_filter_cros.cc",
      "normalizing_input_filter_cros.h",
    ]
  }

  # TODO(GYP) when a nacl version of this target is made, these files
  # won't be part of it.
  sources += [
    "pepper_entrypoints.cc",
    "pepper_entrypoints.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  defines = [ "HAVE_STDINT_H" ]  # Required by on2_integer.h.

  deps = [
    "//net",
    "//ppapi/cpp:objects",
    "//ppapi/cpp/private:internal_module",
    "//remoting/base",
    "//remoting/client",
    "//remoting/codec",
    "//remoting/protocol",
    "//third_party/webrtc/modules/desktop_capture",
    "//ui/events:dom_keycode_converter",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "empty_cursor_filter_unittest.cc",
    "normalizing_input_filter_mac_unittest.cc",
    "touch_input_scaler_unittest.cc",
  ]

  if (is_chromeos) {
    sources += [ "normalizing_input_filter_cros_unittest.cc" ]
  }

  deps = [
    ":plugin",
    "//remoting/proto",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/webrtc",
  ]
}
