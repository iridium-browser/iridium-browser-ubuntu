# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

component("user_manager") {
  sources = [
    "empty_user_info.cc",
    "empty_user_info.h",
    "user_info.cc",
    "user_info.h",
    "user_info_impl.cc",
    "user_info_impl.h",
    "user_manager_export.h",
  ]

  deps = [
    "//base",
    "//components/signin/core/account_id",
    "//skia",
    "//ui/gfx",
  ]

  defines = [ "USER_MANAGER_IMPLEMENTATION" ]

  if (is_chromeos) {
    sources += [
      "known_user.cc",
      "known_user.h",
      "remove_user_delegate.h",
      "user.cc",
      "user.h",
      "user_image/user_image.cc",
      "user_image/user_image.h",
      "user_manager.cc",
      "user_manager.h",
      "user_manager_base.cc",
      "user_manager_base.h",
      "user_type.h",
    ]
    deps += [
      "//components/prefs",
      "//components/session_manager/core",
      "//google_apis",
      "//url",
    ]
  }
}

if (is_chromeos) {
  source_set("test_support") {
    testonly = true
    sources = [
      "fake_user_manager.cc",
      "fake_user_manager.h",
    ]
    deps = [
      ":user_manager",
      "//base",
      "//chromeos:chromeos",
      "//components/signin/core/account_id",
      "//skia",
      "//ui/base",
    ]
  }

  source_set("unit_tests") {
    testonly = true
    sources = [
      "user_unittest.cc",
    ]
    deps = [
      ":user_manager",
      "//components/signin/core/account_id",
      "//skia",
      "//testing/gtest",
      "//ui/gfx",
    ]
  }
}
