# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

tracing_proto_sources = [
  "event.proto",
  "events_chunk.proto",
]

# Stubs generated using custom plugin.
proto_library("protos") {
  visibility = [ "//components/tracing/*" ]

  proto_in_dir = "."
  sources = tracing_proto_sources

  generator_plugin_label =
      "//components/tracing/tools/proto_zero_plugin:proto_zero_plugin"
  generator_plugin_suffix = ".pbzero"
  generator_plugin_options = "wrapper_namespace=pbzero"
  generate_cc = false
  generate_python = false
}

# Official protobuf C++ stubs to test conformance.
proto_library("golden_protos_for_tests") {
  visibility = [ "//components/tracing/*" ]
  testonly = true

  proto_in_dir = "."
  proto_out_dir = "components/tracing/test/golden_protos"
  sources = tracing_proto_sources

  generate_python = false
}
