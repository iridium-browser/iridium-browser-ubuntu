# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# GYP version: components/gcm_driver.gypi:instance_id_driver
source_set("instance_id") {
  sources = [
    "instance_id.cc",
    "instance_id.h",
    "instance_id_driver.cc",
    "instance_id_driver.h",
    "instance_id_impl.cc",
    "instance_id_impl.h",
  ]

  deps = [
    "//base",
    "//crypto",
    "//components/gcm_driver",
  ]

  if (is_android) {
    sources -= [
      "instance_id_impl.cc",
      "instance_id_impl.h",
    ]
    sources += [
      "instance_id_android.cc",
      "instance_id_android.h",
    ]
  }
}

# GYP version: components/gcm_driver.gypi:instance_id_test_support
source_set("test_support") {
  testonly = true
  sources = [
    "fake_gcm_driver_for_instance_id.cc",
    "fake_gcm_driver_for_instance_id.h",
  ]

  deps = [
    ":instance_id",
    "//components/gcm_driver:test_support",
    "//testing/gtest",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "instance_id_driver_unittest.cc",
  ]

  deps = [
    ":instance_id",
    ":test_support",
    "//testing/gtest",
  ]
}
