# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}

# GYP version: components/ntp_tiles.gypi:ntp_tiles
static_library("ntp_tiles") {
  sources = [
    "constants.cc",
    "constants.h",
    "most_visited_sites.cc",
    "most_visited_sites.h",
    "ntp_tile.cc",
    "ntp_tile.h",
    "popular_sites.cc",
    "popular_sites.h",
    "pref_names.cc",
    "pref_names.h",
    "switches.cc",
    "switches.h",
  ]

  public_deps = [
    "//base",
    "//components/history/core/browser",
    "//components/suggestions",
  ]
  deps = [
    "//components/google/core/browser",
    "//components/pref_registry",
    "//components/prefs",
    "//components/search_engines",
    "//components/variations",
    "//components/variations/service",
  ]

  if (is_android) {
    deps += [ "android:ntp_tiles_jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "most_visited_sites_unittest.cc",
  ]

  deps = [
    ":ntp_tiles",
    "//testing/gtest",
  ]
}

if (is_android) {
  java_cpp_enum("ntp_tiles_enums_java") {
    sources = [
      "most_visited_sites.h",
      "ntp_tile.h",
    ]
  }
}
