# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("gles2") {
  visibility = [
    "//components/view_manager:lib",
    "//components/view_manager/surfaces:*",
    "//mojo/runner:lib",  # For android
  ]

  sources = [
    "command_buffer_driver.cc",
    "command_buffer_driver.h",
    "command_buffer_impl.cc",
    "command_buffer_impl.h",
    "command_buffer_impl_observer.h",
    "command_buffer_local.cc",
    "command_buffer_local.h",
    "command_buffer_local_client.h",
    "gpu_impl.cc",
    "gpu_impl.h",
    "gpu_memory_tracker.cc",
    "gpu_memory_tracker.h",
    "gpu_state.cc",
    "gpu_state.h",
  ]

  public_deps = [
    ":lib",
  ]
  deps = [
    "//base",
    "//components/view_manager/public/interfaces",
    "//gpu/command_buffer/client",
    "//gpu/command_buffer/service",
    "//mojo/converters/geometry",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
    "//ui/mojo/geometry:interfaces",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gl",
  ]

  if (is_android) {
    deps += [ "//mojo/platform_handle:defs" ]
  } else {
    deps += [ "//mojo/platform_handle" ]
  }

  include_dirs = [ "../.." ]
}

source_set("lib") {
  sources = [
    "command_buffer_type_conversions.cc",
    "command_buffer_type_conversions.h",
    "mojo_buffer_backing.cc",
    "mojo_buffer_backing.h",
    "mojo_gpu_memory_buffer.cc",
    "mojo_gpu_memory_buffer.h",
    "mojo_gpu_memory_buffer_manager.cc",
    "mojo_gpu_memory_buffer_manager.h",
    "raster_thread_helper.cc",
    "raster_thread_helper.h",
  ]

  deps = [
    "//base",
    "//cc",
    "//components/view_manager/public/interfaces",
    "//gpu/command_buffer/common",
    "//gpu/command_buffer/client",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
    "//third_party/mojo/src/mojo/public/cpp/system",
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]

  include_dirs = [ "../.." ]
}
