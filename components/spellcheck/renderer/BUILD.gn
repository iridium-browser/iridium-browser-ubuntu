# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

source_set("renderer") {
  sources = [
    "custom_dictionary_engine.cc",
    "custom_dictionary_engine.h",
    "hunspell_engine.cc",
    "hunspell_engine.h",
    "platform_spelling_engine.cc",
    "platform_spelling_engine.h",
    "spellcheck.cc",
    "spellcheck.h",
    "spellcheck_language.cc",
    "spellcheck_language.h",
    "spellcheck_provider.cc",
    "spellcheck_provider.h",
    "spellcheck_worditerator.cc",
    "spellcheck_worditerator.h",
    "spelling_engine.h",
  ]

  if (!use_browser_spellchecker) {
    sources -= [
      "platform_spelling_engine.cc",
      "platform_spelling_engine.h",
    ]
  }

  if (is_android) {
    sources -= [
      "hunspell_engine.cc",
      "hunspell_engine.h",
    ]
  }

  deps = [
    "//base:i18n",
    "//components/spellcheck/common",
    "//content/public/renderer",
    "//ipc",
    "//third_party/WebKit/public:blink",
    "//third_party/icu",
  ]

  if (!is_android) {
    deps += [ "//third_party/hunspell" ]
  }

  if (is_win) {
    cflags = [ "/wd4267" ]  # conversion from 'size_t' to 'int' on x64 (crbug.com/633312)
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "custom_dictionary_engine_unittest.cc",
    "spellcheck_multilingual_unittest.cc",
    "spellcheck_provider_hunspell_unittest.cc",
    "spellcheck_provider_mac_unittest.cc",
    "spellcheck_provider_test.cc",
    "spellcheck_provider_test.h",
    "spellcheck_unittest.cc",
    "spellcheck_worditerator_unittest.cc",
  ]
  data = [
    "//third_party/hunspell_dictionaries/",
  ]

  if (is_mac) {
    sources -= [
      # This tests Chrome's spellchecker which Mac doesn't use.
      "spellcheck_multilingual_unittest.cc",
      "spellcheck_provider_hunspell_unittest.cc",
    ]
  }

  if (is_android) {
    sources -= [
      "spellcheck_multilingual_unittest.cc",
      "spellcheck_provider_hunspell_unittest.cc",
      "spellcheck_unittest.cc",
    ]
  }

  deps = [
    ":renderer",
    "//base:i18n",
    "//components/spellcheck/common",
    "//ipc:ipc",
    "//testing/gtest",
    "//third_party/WebKit/public:blink",
    "//third_party/icu",
  ]

  if (is_mac && !is_ios) {
    deps += [ "//third_party/hunspell" ]
  }

  if (is_win) {
    cflags = [ "/wd4267" ]  # conversion from 'size_t' to 'int' on x64 (crbug.com/633312)
  }
}
