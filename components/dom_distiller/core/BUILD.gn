# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# GYP version: components/dom_distiller.gypi:dom_distiller_core
source_set("core") {
  sources = [
    "../android/component_jni_registrar.cc",
    "../android/component_jni_registrar.h",
    "article_attachments_data.cc",
    "article_attachments_data.h",
    "article_distillation_update.cc",
    "article_distillation_update.h",
    "article_entry.cc",
    "article_entry.h",
    "distillable_page_detector.cc",
    "distillable_page_detector.h",
    "distilled_content_store.cc",
    "distilled_content_store.h",
    "distilled_page_prefs.cc",
    "distilled_page_prefs.h",
    "distilled_page_prefs_android.cc",
    "distilled_page_prefs_android.h",
    "distiller.cc",
    "distiller.h",
    "distiller_page.cc",
    "distiller_page.h",
    "distiller_url_fetcher.cc",
    "distiller_url_fetcher.h",
    "dom_distiller_constants.cc",
    "dom_distiller_constants.h",
    "dom_distiller_model.cc",
    "dom_distiller_model.h",
    "dom_distiller_observer.h",
    "dom_distiller_request_view_base.cc",
    "dom_distiller_request_view_base.h",
    "dom_distiller_service.cc",
    "dom_distiller_service.h",
    "dom_distiller_store.cc",
    "dom_distiller_store.h",
    "dom_distiller_switches.cc",
    "dom_distiller_switches.h",
    "experiments.cc",
    "experiments.h",
    "external_feedback_reporter.h",
    "feedback_reporter.cc",
    "feedback_reporter.h",
    "page_features.cc",
    "page_features.h",
    "task_tracker.cc",
    "task_tracker.h",
    "url_constants.cc",
    "url_constants.h",
    "url_utils.cc",
    "url_utils.h",
    "url_utils_android.cc",
    "url_utils_android.h",
    "viewer.cc",
    "viewer.h",
  ]

  public_deps = [
    "//components/dom_distiller/core/proto",
    "//third_party/dom_distiller_js:proto",
  ]
  deps = [
    "//base",
    "//base:prefs",
    "//components/leveldb_proto",
    "//components/pref_registry",
    "//components/resources",
    "//components/strings",
    "//net",
    "//skia",
    "//sync",
    "//sync/protocol",  # TODO(brettw) bug 475091: this should be unnecessary.
    "//third_party/re2",
    "//ui/base",
    "//url",
  ]

  if (is_android) {
    sources += [
      "dom_distiller_service_android.cc",
      "dom_distiller_service_android.h",
    ]
    deps += [ ":jni_headers" ]
  }
}

# GYP version: components/dom_distiller.gypi:dom_distiller_test_support
source_set("test_support") {
  testonly = true
  sources = [
    "dom_distiller_test_util.cc",
    "dom_distiller_test_util.h",
    "fake_distiller.cc",
    "fake_distiller.h",
    "fake_distiller_page.cc",
    "fake_distiller_page.h",
  ]

  deps = [
    ":core",
    "//base",
    "//components/leveldb_proto:test_support",
    "//sync",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "article_entry_unittest.cc",
    "distilled_content_store_unittest.cc",
    "distilled_page_prefs_unittests.cc",
    "distiller_unittest.cc",
    "distiller_url_fetcher_unittest.cc",
    "dom_distiller_model_unittest.cc",
    "dom_distiller_service_unittest.cc",
    "dom_distiller_store_unittest.cc",
    "page_features_unittest.cc",
    "task_tracker_unittest.cc",
    "url_utils_unittest.cc",
    "viewer_unittest.cc",
  ]

  deps = [
    ":core",
    ":test_support",
    "//base",
    "//components/leveldb_proto:test_support",
    "//components/pref_registry:test_support",
    "//net:test_support",
    "//sync",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]
}

if (is_android) {
  import("//build/config/android/rules.gni")

  generate_jni("jni_headers") {
    sources = [
      "../android/java/src/org/chromium/components/dom_distiller/core/DistilledPagePrefs.java",
      "../android/java/src/org/chromium/components/dom_distiller/core/DomDistillerService.java",
      "../android/java/src/org/chromium/components/dom_distiller/core/DomDistillerUrlUtils.java",
    ]
    jni_package = "dom_distiller_core"
  }
}
