# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ios/rules.gni")
import("//build/config/mac/base_rules.gni")
import("//build/mac/tweak_info_plist.gni")
import("//ios/build/chrome_build.gni")
import("//ios/public/provider/chrome/browser/build_config.gni")

tweak_info_plist("info_plist") {
  info_plists = [
    "//ios/chrome/app/resources/Info.plist",
    "//ios/chrome/app/resources/ChromeAddition+Info.plist",
  ]
  if (ios_chrome_info_plist_additions != []) {
    info_plists += ios_chrome_info_plist_additions
  }
  if (ios_encryption_export_compliance_code != "") {
    info_plists +=
        [ "//ios/chrome/app/resources/EncryptionExportCompliance+Info.plist" ]
  }
  args = [
    "--breakpad=$breakpad_enabled_as_int",
    "--branding=$chromium_short_name",
  ]
}

ios_app_bundle("chrome_clean_skeleton") {
  output_name = "CSChromium"

  entitlements_path = "//ios/chrome/app/resources/Chrome.entitlements"
  info_plist_target = ":info_plist"

  deps = [
    ":main",
    "//ios/chrome/app:tests_fake_hook",
  ]

  bundle_deps = [ "//ios/chrome/app/resources" ]

  extra_substitutions = [
    "CHROMIUM_BUNDLE_ID=$chromium_bundle_id",
    "CHROMIUM_SHORT_NAME=$chromium_short_name",
    "CHROMIUM_URL_SCHEME_1=$url_unsecure_scheme",
    "CHROMIUM_URL_SCHEME_2=$url_secure_scheme",
    "CHROMIUM_URL_SCHEME_3=$url_x_callback_scheme",
    "CHROMIUM_URL_SCHEME_4=$url_channel_scheme",
  ]

  if (ios_encryption_export_compliance_code != "") {
    extra_substitutions += [ "ENCRYPTION_EXPORT_COMPLIANCE_CODE=" +
                             ios_encryption_export_compliance_code ]
  }
}

source_set("main") {
  sources = [
    "main.mm",
  ]

  deps = [
    ":app",
    "//base",
    "//components/crash/core/common",
    "//ios/chrome/app:app_internal",
    "//ios/chrome/app/startup",
    "//ios/chrome/browser",
    "//ios/chrome/browser:browser_internal",
    "//ios/chrome/browser/crash_report",
    "//ios/chrome/common",
    "//third_party/google_toolbox_for_mac",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]
}

source_set("application_state") {
  sources = [
    "application_phase.h",
    "application_state.h",
    "application_state.mm",
    "application_step.h",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    "//base",
    "//ios/clean/chrome/browser",
  ]
}

source_set("app") {
  sources = [
    "app_delegate.h",
    "app_delegate.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":application_state",
    "//ios/clean/chrome/app/steps",
  ]
}
