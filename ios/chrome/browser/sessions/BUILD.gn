# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("sessions") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "ios_chrome_session_tab_helper.h",
    "ios_chrome_session_tab_helper.mm",
    "ios_chrome_tab_restore_service_client.h",
    "ios_chrome_tab_restore_service_client.mm",
    "ios_chrome_tab_restore_service_factory.cc",
    "ios_chrome_tab_restore_service_factory.h",
  ]
  deps = [
    "//base",
    "//components/keyed_service/ios",
    "//components/sessions",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/tabs",
    "//ios/public/provider/chrome/browser",
    "//ios/web",
    "//url",
  ]
  public_deps = [
    ":sessions_non_arc",
  ]
}

source_set("sessions_non_arc") {
  visibility = [ ":sessions" ]
  sources = [
    "NSCoder+Compatibility.h",
    "NSCoder+Compatibility.mm",
    "session_service.h",
    "session_service.mm",
    "session_util.h",
    "session_util.mm",
    "session_window.h",
    "session_window.mm",
    "tab_restore_service_delegate_impl_ios.h",
    "tab_restore_service_delegate_impl_ios.mm",
    "tab_restore_service_delegate_impl_ios_factory.h",
    "tab_restore_service_delegate_impl_ios_factory.mm",
  ]
  deps = [
    "//base",
    "//components/keyed_service/core",
    "//components/keyed_service/ios",
    "//components/sessions",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/tabs",
    "//ios/web",
    "//net",
  ]
  libs = [ "UIKit.framework" ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "test_session_service.h",
    "test_session_service.mm",
  ]
  deps = [
    ":sessions",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "session_service_unittest.mm",
    "session_window_unittest.mm",
  ]
  deps = [
    ":resources_unit_tests",
    ":sessions",
    "//base",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/web",
    "//ios/web:test_support",
    "//testing/gtest",
    "//third_party/ocmock",
  ]
}

bundle_data("resources_unit_tests") {
  visibility = [ ":unit_tests" ]
  testonly = true
  sources = [
    "//ios/chrome/test/data/sessions/corrupted.plist",
  ]
  outputs = [
    "{{bundle_resources_dir}}/ios/chrome/test/data/sessions/{{source_file_part}}",
  ]
}
