# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

action("aggregate_vector_icons") {
  visibility = [ ":*" ]

  script = "//ui/gfx/vector_icons/aggregate_vector_icons.py"

  icons = [
    "palette_action_capture_region.1x.icon",
    "palette_action_capture_region.icon",
    "palette_action_capture_screen.1x.icon",
    "palette_action_capture_screen.icon",
    "palette_action_create_note.1x.icon",
    "palette_action_create_note.icon",
    "palette_mode_laser_pointer.1x.icon",
    "palette_mode_laser_pointer.icon",
    "palette_mode_magnify.1x.icon",
    "palette_mode_magnify.icon",
    "palette_tray_icon_capture_region.1x.icon",
    "palette_tray_icon_capture_region.icon",
    "palette_tray_icon_default.1x.icon",
    "palette_tray_icon_default.icon",
    "palette_tray_icon_laser_pointer.1x.icon",
    "palette_tray_icon_laser_pointer.icon",
    "palette_tray_icon_magnify.1x.icon",
    "palette_tray_icon_magnify.icon",
    "shelf_app_list.1x.icon",
    "shelf_app_list.icon",
    "shelf_notifications.1x.icon",
    "shelf_notifications.icon",
    "system_menu_accessibility.1x.icon",
    "system_menu_accessibility.icon",
    "system_menu_arrow_right.1x.icon",
    "system_menu_arrow_right.icon",
    "system_menu_bluetooth.1x.icon",
    "system_menu_bluetooth.icon",
    "system_menu_bluetooth_disabled.1x.icon",
    "system_menu_bluetooth_disabled.icon",
    "system_menu_brightness.1x.icon",
    "system_menu_brightness.icon",
    "system_menu_business.1x.icon",
    "system_menu_business.icon",
    "system_menu_caps_lock.1x.icon",
    "system_menu_caps_lock.icon",
    "system_menu_cast.1x.icon",
    "system_menu_cast.icon",
    "system_menu_child_user.1x.icon",
    "system_menu_child_user.icon",
    "system_menu_keyboard.1x.icon",
    "system_menu_keyboard.icon",
    "system_menu_rotation_lock_auto.1x.icon",
    "system_menu_rotation_lock_auto.icon",
    "system_menu_rotation_lock_locked.1x.icon",
    "system_menu_rotation_lock_locked.icon",
    "system_menu_screen_share.1x.icon",
    "system_menu_screen_share.icon",
    "system_menu_sms.icon",
    "system_menu_supervised_user.1x.icon",
    "system_menu_supervised_user.icon",
    "system_menu_timer.1x.icon",
    "system_menu_timer.icon",
    "system_menu_update.1x.icon",
    "system_menu_update.icon",
    "system_tray_accessibility.1x.icon",
    "system_tray_accessibility.icon",
    "system_tray_battery.1x.icon",
    "system_tray_battery.icon",
    "system_tray_battery_alert.1x.icon",
    "system_tray_battery_alert.icon",
    "system_tray_battery_bolt.1x.icon",
    "system_tray_battery_bolt.icon",
    "system_tray_battery_unreliable.1x.icon",
    "system_tray_battery_unreliable.icon",
    "system_tray_battery_x.1x.icon",
    "system_tray_battery_x.icon",
    "system_tray_caps_lock.1x.icon",
    "system_tray_caps_lock.icon",
    "system_tray_cast.1x.icon",
    "system_tray_cast.icon",
    "system_tray_rotation_lock_locked.1x.icon",
    "system_tray_rotation_lock_locked.icon",
    "system_tray_screen_share.1x.icon",
    "system_tray_screen_share.icon",
    "system_tray_update.1x.icon",
    "system_tray_update.icon",
    "system_tray_volume_mute.1x.icon",
    "system_tray_volume_mute.icon",
  ]

  output_cc = "$target_gen_dir/vector_icons.cc"
  output_h = "$target_gen_dir/vector_icons.h"

  inputs = icons
  inputs += [
    "vector_icons.cc.template",
    "vector_icons.h.template",
  ]
  outputs = [
    output_cc,
    output_h,
  ]

  response_file_contents = rebase_path(icons, root_build_dir)

  args = [
    "--working_directory=" + rebase_path("./"),
    "--file_list={{response_file_name}}",
    "--output_cc=" + rebase_path(output_cc, root_build_dir),
    "--output_h=" + rebase_path(output_h, root_build_dir),
  ]
}

source_set("vector_icons") {
  sources = get_target_outputs(":aggregate_vector_icons")
  sources += [ "//ui/gfx/vector_icon_types.h" ]

  deps = [
    ":aggregate_vector_icons",
    "//base",
    "//skia",
  ]
}
