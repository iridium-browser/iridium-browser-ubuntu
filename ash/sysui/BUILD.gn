# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//services/shell/public/cpp/service.gni")
import("//services/shell/public/service_manifest.gni")

source_set("lib") {
  sources = [
    "app_list_presenter_mus.cc",
    "app_list_presenter_mus.h",
    "keyboard_ui_mus.cc",
    "keyboard_ui_mus.h",
    "shelf_delegate_mus.cc",
    "shelf_delegate_mus.h",
    "shell_delegate_mus.cc",
    "shell_delegate_mus.h",
    "stub_context_factory.cc",
    "stub_context_factory.h",
    "sysui_application.cc",
    "sysui_application.h",
    "wallpaper_delegate_mus.cc",
    "wallpaper_delegate_mus.h",
  ]

  deps = [
    "//ash",
    "//ash/public/interfaces",
    "//cc",
    "//cc/surfaces",
    "//components/user_manager",
    "//components/wallpaper",
    "//device/bluetooth",
    "//mojo/common:common_base",
    "//services/catalog/public/cpp",
    "//services/shell/public/cpp",
    "//services/tracing/public/cpp",
    "//services/ui/common:mus_common",
    "//services/ui/public/cpp",
    "//services/ui/public/cpp/input_devices",
    "//services/ui/public/interfaces",
    "//ui/app_list/presenter",
    "//ui/app_list/presenter:mojom",
    "//ui/aura",
    "//ui/events",
    "//ui/events/devices",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gfx/geometry/mojo",
    "//ui/keyboard:mojom",
    "//ui/message_center",
    "//ui/platform_window/stub",
    "//ui/resources",
    "//ui/strings",
    "//ui/views",
    "//ui/views/mus:for_mojo_application",
    "//ui/wm",
  ]

  public_deps = [
    "//ash/public/interfaces",
    "//mash/shelf/public/interfaces",
  ]

  if (is_chromeos) {
    deps += [ "//chromeos" ]
  }
}

service_manifest("manifest") {
  name = "ash_sysui"
  source = "manifest.json"
}

service("sysui") {
  output_name = "ash_sysui"
  sources = [
    "main.cc",
  ]
  deps = [
    ":lib",
    "//ash/common/strings:ash_test_strings",
    "//ash/resources:ash_test_resources_100_percent",
    "//ash/resources:ash_test_resources_200_percent",
    "//services/shell/public/cpp",
  ]

  data_deps = [
    ":manifest",
    "//services/ui",
  ]

  # TODO: Load locale-specific strings and explicit resource dependencies.
  resources = [
    "$root_out_dir/ash_test_resources_100_percent.pak",
    "$root_out_dir/ash_test_resources_200_percent.pak",
    "$root_out_dir/ash_test_strings.pak",
  ]
}
