# Copyright (c) 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/version.gni")
import("//testing/test.gni")

if (is_win) {
  config("xp") {
    # Target low-end CPUs on XP.
    cflags = [ "/arch:IA32" ]
    defines = [
      "NTDDI_VERSION=0x05010200",
      "WINVER=0x0501",
      "_WIN32_WINNT=0x0501",
    ]
  }

  executable("disable_outdated_build_detector") {
    sources = [
      "disable_outdated_build_detector_main.cc",
    ]

    configs -= [
      "//build/config/win:console",
      "//build/config/win:winver",
    ]
    configs += [
      "//build/config/win:windowed",
      ":xp",
    ]

    deps = [
      ":disable_outdated_build_detector_exe_version",
      ":lib",
      "//build/win:default_exe_manifest",
    ]
  }

  process_version("disable_outdated_build_detector_exe_version") {
    template_file = "disable_outdated_build_detector_exe_version.rc.version"
    output = "$target_gen_dir/disable_outdated_build_detector_exe_version.rc"
  }

  source_set("lib") {
    sources = [
      "constants.cc",
      "constants.h",
      "disable_outdated_build_detector.cc",
      "disable_outdated_build_detector.h",
      "google_update_integration.cc",
      "google_update_integration.h",
    ]
    configs -= [ "//build/config/win:winver" ]
    configs += [ ":xp" ]
  }

  test("disable_outdated_build_detector_unittests") {
    sources = [
      "disable_outdated_build_detector_unittest.cc",
      "google_update_integration_unittest.cc",
      "run_all_unittests.cc",
    ]

    deps = [
      ":lib",
      "//base",
      "//base/test:test_support",
      "//chrome/installer/util:with_no_strings",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}
