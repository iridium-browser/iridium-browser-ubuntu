# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//build/config/mac/rules.gni")

source_set("mac_installer_base") {
  sources = [
    "Downloader.m",
    "NSAlert+ChromeInstallerAdditions.m",
    "NSError+ChromeInstallerAdditions.m",
    "OmahaCommunication.m",
    "OmahaXMLParser.m",
    "OmahaXMLRequest.m",
    "SystemInfo.m",
  ]
}

mac_app_bundle("mac_installer_app") {
  info_plist = "Info.plist"
  extra_substitutions = [ "MACOSX_DEPLOYMENT_TARGET=10.9" ]
  sources = [
    "AppDelegate.m",
    "AuthorizedInstall.m",
    "InstallerWindowController.m",
    "main.m",
  ]

  deps = [
    ":mac_installer_base",
    ":mac_installer_resources",
    ":mac_installer_xibs",
  ]

  libs = [
    "AppKit.framework",
    "CoreFoundation.framework",
    "Security.framework",
  ]
}

mac_xib_bundle_data("mac_installer_xibs") {
  sources = [
    "MainMenu.xib",
  ]
}

bundle_data("mac_installer_resources") {
  sources = [
    "copy_to_disk.sh",
  ]
  outputs = [
    "{{bundle_resources_dir}}/copy_to_disk.sh",
  ]
}

test("mac_installer_unittests") {
  sources = [
    "testing/OmahaXMLRequest_test.mm",
    "testing/SystemInfo_test.mm",
  ]
  deps = [
    ":mac_installer_base",
    "//base:base",
    "//base/test:run_all_unittests",
    "//testing/gtest:gtest",
  ]
  libs = [ "Foundation.framework" ]
  data = [
    "testing/requestCheck.dtd",
    "testing/requestSample.xml",
    "testing/responseExample.xml",
  ]
}
