# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
import("//testing/test.gni")
import("//third_party/mojo/src/mojo/public/tools/bindings/mojom.gni")

gypi_values = exec_script("//build/gypi_to_gn.py",
                          [ rebase_path("media_router.gypi") ],
                          "scope",
                          [ "media_router.gypi" ])

# TODO(mfoltz): Fix problem that requires explicitly listing //skia
static_library("router") {
  deps = [
    "//base",
    "//chrome/common:constants",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//skia",
    "//url",
  ]
  sources = rebase_path(gypi_values.media_router_sources,
                        ".",
                        "//chrome/browser/media/router")
  if (!is_android) {
    deps += [
      ":mojo_bindings",
      "//extensions/browser",
      "//third_party/mojo/src/mojo/public/cpp/bindings",
    ]
    sources += rebase_path(gypi_values.media_router_non_android_sources,
                           ".",
                           "//chrome/browser/media/router")
  }
}

mojom("mojo_bindings") {
  sources = [
    "media_router.mojom",
  ]
}

source_set("test_support") {
  testonly = true
  deps = [
    ":mojo_bindings",
    ":router",
    "//chrome/test:test_support",
    "//testing/gmock",
  ]
  sources = rebase_path(gypi_values.media_router_test_support_sources,
                        ".",
                        "//chrome/browser/media/router")
}
