# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#

import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

static_library("router") {
  deps = [
    "//base",
    "//chrome/common:constants",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//content/public/common",
    "//third_party/icu",
    "//url",
  ]
  sources = [
    "browser_presentation_connection_proxy.cc",
    "browser_presentation_connection_proxy.h",
    "create_presentation_connection_request.cc",
    "create_presentation_connection_request.h",
    "issue.cc",
    "issue.h",
    "issue_manager.cc",
    "issue_manager.h",
    "issues_observer.cc",
    "issues_observer.h",
    "media_route.cc",
    "media_route.h",
    "media_router.h",
    "media_router_base.cc",
    "media_router_base.h",
    "media_router_dialog_controller.cc",
    "media_router_dialog_controller.h",
    "media_router_factory.cc",
    "media_router_factory.h",
    "media_router_metrics.cc",
    "media_router_metrics.h",
    "media_routes_observer.cc",
    "media_routes_observer.h",
    "media_sink.cc",
    "media_sink.h",
    "media_sink_service.cc",
    "media_sink_service.h",
    "media_sinks_observer.cc",
    "media_sinks_observer.h",
    "media_source.cc",
    "media_source.h",
    "media_source_helper.cc",
    "media_source_helper.h",
    "offscreen_presentation_manager.cc",
    "offscreen_presentation_manager.h",
    "offscreen_presentation_manager_factory.cc",
    "offscreen_presentation_manager_factory.h",
    "presentation_media_sinks_observer.cc",
    "presentation_media_sinks_observer.h",
    "presentation_request.cc",
    "presentation_request.h",
    "presentation_service_delegate_impl.cc",
    "presentation_service_delegate_impl.h",
    "presentation_service_delegate_observers.cc",
    "presentation_service_delegate_observers.h",
    "receiver_presentation_service_delegate_impl.cc",
    "receiver_presentation_service_delegate_impl.h",
    "render_frame_host_id.h",
    "route_message.cc",
    "route_message.h",
    "route_message_observer.cc",
    "route_message_observer.h",
    "route_request_result.cc",
    "route_request_result.h",
  ]

  if (!is_android) {
    deps += [
      ":mojo_bindings",
      "//extensions/browser",
      "//mojo/public/cpp/bindings",
    ]
    sources += [
      # TODO(crbug.com/646627): Move media_router_ui_service* to
      # chrome/browser/ui/.
      "media_router_ui_service.cc",
      "media_router_ui_service.h",
      "media_router_ui_service_factory.cc",
      "media_router_ui_service_factory.h",
      "mojo/media_route_provider_util_win.cc",
      "mojo/media_route_provider_util_win.h",
      "mojo/media_router_mojo_impl.cc",
      "mojo/media_router_mojo_impl.h",
      "mojo/media_router_mojo_metrics.cc",
      "mojo/media_router_mojo_metrics.h",
    ]
  }
}

mojom("mojo_bindings") {
  sources = [
    "mojo/media_router.mojom",
  ]

  public_deps = [
    "//mojo/common:common_custom_types",
    "//url/mojo:url_mojom_origin",
  ]
}

static_library("test_support") {
  testonly = true
  deps = [
    ":router",
    "//chrome/test:test_support",
    "//testing/gmock",
  ]
  sources = [
    "mock_media_router.cc",
    "mock_media_router.h",
    "mock_screen_availability_listener.cc",
    "mock_screen_availability_listener.h",
    "test_helper.cc",
    "test_helper.h",
  ]

  if (!is_android) {
    deps += [
      ":mojo_bindings",
      "//extensions/common",
    ]
    sources += [
      "mojo/media_router_mojo_test.cc",
      "mojo/media_router_mojo_test.h",
    ]
  }
}
