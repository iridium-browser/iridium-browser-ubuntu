# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

# Generates monochrome application class.
#
# Variables
#   monochrome_application_class: the name of monochrome application class.
#   package: the package name of monochrome class.
#   super_class: the super class of monochrome application.
#   output: the generated application class.
template("generate_monochrome_application") {
  jinja_template(target_name) {
    forward_variables_from(invoker,
                           [
                             "output",
                             "monochrome_application_class",
                             "package",
                             "super_class",
                           ])
    assert(defined(output))
    assert(defined(monochrome_application_class))
    assert(defined(package))
    assert(defined(super_class))
    input = "//chrome/android/java/templates/MonochromeApplication.template"
    variables = [
      "package=${package}",
      "super_class=${super_class}",
      "monochrome_application_class=${monochrome_application_class}",
    ]
  }
}
