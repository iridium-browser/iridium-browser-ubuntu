# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/crypto.gni")
import("//build/config/features.gni")
import("//extensions/extensions.gni")
import("//third_party/mojo/src/mojo/public/tools/bindings/mojom.gni")

# GYP version: extensions/extensions.gyp:extensions_common_constants
source_set("common_constants") {
  sources =
      rebase_path(extensions_gypi_values.extensions_common_constants_sources,
                  ".",
                  "//extensions")

  # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]
}

if (enable_extensions) {
  mojom("mojo") {
    sources = rebase_path(extensions_gypi_values.extensions_common_mojo_sources,
                          ".",
                          "//extensions")
  }

  # GYP version: extensions/extensions.gyp:extensions_common
  # This must be a static library because extensions common depends on
  # GetTrustedICAPublicKey in extensions/browser which isn't always linked
  # in. TODO(brettw): This reverse dependency should be fixed.
  static_library("common") {
    sources = rebase_path(extensions_gypi_values.extensions_common_sources,
                          ".",
                          "//extensions")

    # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
    configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

    deps = [
      ":common_constants",
      ":mojo",

      # TODO(benwells): figure out what to do with the api target and
      # api resources compiled into the chrome resource bundle.
      # http://crbug.com/162530
      "//chrome:resources",
      "//components/crx_file",
      "//components/url_matcher",
      "//content/public/common",
      "//crypto",
      "//device/bluetooth",
      "//device/usb",
      "//extensions/common/api",
      "//extensions/strings",
      "//extensions:extensions_resources",
      "//ipc",
      "//net",
      "//third_party/icu",
      "//third_party/libxml",
      "//third_party/re2",
      "//ui/base",
      "//ui/gfx/geometry",
      "//ui/gfx/ipc",
      "//url",
    ]

    if (use_openssl) {
      sources +=
          rebase_path(extensions_gypi_values.extensions_common_sources_openssl,
                      ".",
                      "//extensions")
      deps += [ "//third_party/boringssl" ]
    } else {
      sources += rebase_path(
              extensions_gypi_values.extensions_common_sources_nonopenssl,
              ".",
              "//extensions")
    }

    if (enable_nacl) {
      nacl_sources =
          rebase_path(extensions_gypi_values.extensions_common_sources_nacl,
                      ".",
                      "//extensions")
      sources += nacl_sources
    }
  }
}  # enable_extensions
